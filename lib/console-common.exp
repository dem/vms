#!/usr/bin/expect -f
# Common procedures for console interaction

set timeout 900

proc connect_console {vm_name} {
    global spawn_id

    log_user 1
    fconfigure stdout -buffering none

    spawn virsh -c qemu:///system console $vm_name

    expect {
        "Escape character" { sleep 2; send "\r" }
        timeout { puts "Timeout waiting for console"; exit 1 }
    }

    expect {
        -re "login:" { sleep 1; send "root\r"; sleep 3 }
        -re "#" { sleep 1 }
        timeout { puts "Timeout waiting for shell"; exit 1 }
    }

    # Disable zsh autocorrect, set dumb terminal and clean prompt
    send "unsetopt correct 2>/dev/null; export TERM=dumb PS1='READY# '\r"
    sleep 1
    send "\r"
    expect {
        "READY#" { }
        timeout { puts "Timeout waiting for READY#"; exit 1 }
    }
    sleep 1
}

proc wait_for_marker {marker_done marker_fail} {
    expect {
        "\n$marker_done" { puts "\n=== Command completed successfully ==="; sleep 1 }
        "\r\n$marker_done" { puts "\n=== Command completed successfully ==="; sleep 1 }
        "\n$marker_fail" { puts "\n=== Command failed ==="; send "\x1d"; expect eof; exit 1 }
        "\r\n$marker_fail" { puts "\n=== Command failed ==="; send "\x1d"; expect eof; exit 1 }
        timeout { puts "Timeout waiting for completion"; send "\x1d"; expect eof; exit 1 }
    }
}

proc disconnect {} {
    send "\x1d"
    expect eof
}
